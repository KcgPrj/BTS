buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
        kotlinVersion = '1.0.3'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
        classpath "com.moowork.gradle:gradle-gulp-plugin:0.13"
    }
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'
apply plugin: "com.moowork.node"
apply plugin: "com.moowork.gulp"

node {
    version = '6.7.0' //使用するnodeのバージョン
    download = true //各自の環境にダウンロードする
}

gulp {
    colors = true //ターミナルに色を出力
}

// makes sure on each build that gulp is installed
gulp_build.dependsOn 'installGulp'

// processes your package.json before running gulp build
gulp_build.dependsOn 'npmInstall'

// runs "gulp build" as part of your gradle build
build.dependsOn gulp_build

jar {
    baseName = 'uiserver'
    version = '0.0.1-SNAPSHOT'
}

dependencies {
    compile('org.springframework.cloud:spring-cloud-starter-oauth2')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
    compile project(":Message")
    compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.SR2"
    }
}

//node_modulesを削除するタスク
//cleanするたびに削除するとビルド時間が辛くなるので依存関係は作らない方針で
task cleanNodeModules(type: Delete) {
    project.delete('node_modules')
}